// Community Power Accelerator macro using CPA example.sam

// Now use template file
/*************************************/
/**           Runtime Tools         **/

// Save the current dir for later use
curDir = userlocaldatadir();
//outln( curDir );

// Determine screen size and set default pop-up positions

scrn = scrnres();

defaultX = scrn[0] - scrn[0]/2;
defaultY = 60;

// exit function (and removes any highlighting)
function exitIf( exitCondition ){
	if( exitCondition ){
		transp();
		exit;
	}
}

function send_excel( xl, variable)
{
	xl_set( xl, value(variable), variable );
}


// if one is open close
close_project();


/*************************************/
/**         Welcome the User        **/

wS = [500,500]; 
//sdt - added description of SAM currency discrepancies
exitIf( !msgbox('Welcome to the community power accelerator example wizard!\n'+
	            'Follow the prompts to to model an example of a community power accelerator project in Phoenix, AZ.\n\n' +
			    'When you finish using the wizard, you can modify the inputs in the resulting .sam file for your own project.\n\n'+
			    'The wizard sets the weather file for Phoenix, AZ, and then prompts you to enter community power accelerator meaningful benefits. Then, a pdf report and a cash flow are generated.\n\n'+
			    'The report and cash flow gives insights for your community power accelerator project.\n\n'+
			    'This wizard is an initial implementation intended as a proof-of-concept. If you have questions or suggestions for improving the wizard, please contact us at sam.support@nrel.gov', 
			 [defaultX, defaultY, 500, 500]) );
			 

/*************************************/
/**         Case management         **/
// Check if there is an active case
//  - if there is, ask the user if they would like to use this case, or else create a new case
/*
if( active_case() != "" )
	newCase = yesno("First, would you like to create a new case?\n\n(selecting 'no' will make changes to the currently active case)",
		[defaultX, defaultY]);
else
	newCase = true;


// If user chooses to use the current case, ensure the right models are selected
if( newCase == false){
	cnf =  configuration();
	
	if( !(strcmp(cnf[0], "PVWatts") == 0 && strcmp(cnf[1],"Community Solar") == 0) ){
		exitIf(! msgbox("The currently active case does not use the appropriate physical and/or financial model for this wizard.\n\nPlease create a new case.",
			"WARNING",
			[defaultX, defaultY, 500, 500]));
		newCase = true;
	}
} 

// Create a new case if required
if( newCase == true ) {
	tmpName = in("Give your case a name", 
				"CPA Wizard", 
				"Create a new case",
				[defaultX, defaultY]);
	exitIf( tmpName == "" );

	if( !create_case('PVWatts', 'Community Solar', tmpName) ){
		msgbox( 'Something went wrong' );
		exit;
	}
}
caseName = case_name();
*/
template_file = runtimedir() + "quickstart/CPA-example.sam";
user_file = curDir + "/CPA example.sam";
copy_file(template_file, user_file, true);
exitIf (!open_project(user_file));

caseName = case_name();
exitIf(caseName == "");

/*************************************/
/**     Get Simulation Location     **/

// Show solar resource page
show_page( 'Location and Resource');

// Highlight resource selection box in UI
focusto('GroupBox2');
geom = widgetpos('GroupBox2');
transp( geom, 'yellow', 70 );

exitIf( !msgbox('Weather data set for Phoenix, AZ.\n'+
       'We have selected a pre-downloaded weather file for this example.\n\n'+
       'You can use the tools on the Location and Resource page to download\n'+
       'your own weather file after the wizard finishes.\n\n'+
       'Click OK to continue.') );

/*************************************/
/**     Get Community Solar information    **/

show_page('Community Solar');

// Energy savings to subscriber
focusto('subscriber1_bill_credit_rate');
geom = widgetpos('subscriber1_bill_credit_rate');
transp( geom, 'yellow', 35 );
energysavings = in( 'Energy savings to subscribers.\n\n'+
                  'Type energy savings in $/kWh for the subscriber. click OK to continue.', 
				'0.1', 'community power accelerator energy savings to subscriber',
			   [defaultX, defaultY] );
exitIf(energysavings == '');
x=alloc(1);
x[0] = to_real(energysavings);
set('subscriber1_bill_credit_rate', x );
transp();

// Lease payment to owner
focusto('subscriber1_payment_annual');
geom = widgetpos('subscriber1_payment_annual');
transp( geom, 'yellow', 35 );
leasepayment = in( 'Lease payment to owner.\n\n'+
                  'Type lease payment in $/yr to the owner. click OK to continue.', 
				'50000', 'community power accelerator lease payment to owner',
			   [defaultX, defaultY] );
exitIf(leasepayment == '');
x[0] = to_real(leasepayment);
set('subscriber1_payment_annual', x );
transp();

// Community benefits payment and job training combined into cs_cost_recurring_fixed
focusto('cs_cost_recurring_fixed');
geom = widgetpos('cs_cost_recurring_fixed');
transp( geom, 'yellow', 35 );
communitybenefitspayments = in( 'Community benefits payment.\n\n'+
                  'Type community benefits payment in $/yr. click OK to continue.', 
				'5000', 'community power accelerator community benefits payment',
			   [defaultX, defaultY] );
exitIf(communitybenefitspayments == '');
x[0] = to_real(communitybenefitspayments);

jobtraining = in( 'Job training value.\n\n'+
                  'Type job training value in $/yr. click OK to continue.', 
				'25000', 'community power accelerator job training value',
			   [defaultX, defaultY] );
exitIf(jobtraining == '');
x[0] += to_real(jobtraining);

set('cs_cost_recurring_fixed', x );
transp();



/*************************************/
/**            Set Costs           **/

//show_page('Installation Costs');

/* Default community solar costs */

// Review time
//millisleep(1000);
//transp();


/*************************************/
/**            Set Finances         **/
//show_page('Financial Parameters');

// Default Commercial Financial parameters
// Review time
//millisleep(1000);
//transp();


/*************************************/
/**            Simulate             **/

// simulate
simulate();

/*************************************/
/**            Outputs              **/

// export cashflow to Excel
export_cashflow_excel();

// create pdf report quickstart/CPA-example.samreport 
	if( yesno("This report will be saved to:\n" + homedir()+"/CPA-Example_Report.pdf\n\nSelect 'Yes' to continue or 'No' to enter a different file path",[defaultX, defaultY]) ){
		reportFile = homedir()+"/CPA-Example_Report.pdf";
	} else {
		reportFile = choose_file( homedir(), "Output report file", 'PDF Files (*.pdf)|*.pdf');
	}
	
	ok = pdfreport( reportFile, {'template'=runtimedir() + 'quickstart/CPA-example.samreport'});
	
	// Open Report
	browse(reportFile);

// create HTML table showing Benefits
case = case_name();

/* cash flow line items from Community Solar outputs with 
1.	Energy savings to subscriber  (Bill credits)
2.	Lease payment to owner (subscription revenue)
3.	Community benefits payments (upfront and recurring)
4.	Job training (upfront and recurring)
*/

	fixedjobtraining = to_real(jobtraining);
	fixedcommunitybenefitspayments = to_real(communitybenefitspayments);
	fixedtotal = fixedjobtraining + fixedcommunitybenefitspayments;

	varbenefits = ['Energy savings to subscriber','Lease payments to owner','Community benefits payments','Job training'];
	varcf = ['cf_subscriber1_bill_credit_amount','cf_community_solar_subscriber1_revenue','cf_community_solar_recurring_fixed','cf_community_solar_recurring_fixed'];
	varmultiplier = [1.0,1.0,fixedcommunitybenefitspayments/fixedtotal,fixedjobtraining/fixedtotal];
    str_html = '<table bgcolor=#dddddd width=100%>';
    ap = round(to_real(get('analysis_period')));

    str_html += '<tr><th>year</th>';
    for (i=0;i<=ap;i++)
    {
		str_html +=  '<th>' + sprintf("%d",i) + '</th>';
	}
    str_html += '</tr>';

    for (i=0;i<#varcf;i++)
    {
		str_html += '<tr  bgcolor=#ffffff><td align="left">' + varbenefits[i] + ' ($)</td>';
		cfline = get(varcf[i]);
		for (j=0; j<#cfline; j++)
		{
			str_html += '<td align="right">' + sprintf('%.0f',varmultiplier[i] * round(cfline[j])) + '</td>';
		}
		str_html += '</tr>';
    }
	str_html +=  '</table></body></html>';


html_dialog ( str_html , 'Benefits for "' + case + '" Case' , [300,300,2600,600]);


